plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'org.jetbrains.kotlin.plugin.serialization' version '1.6.10'
    id 'maven-publish'
    id 'signing'
}

task sourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs
}

android {
    namespace rootProject.ext["GROUP"]
    compileSdk 33

    defaultConfig {
        minSdk 24
        targetSdk 33

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.9.0'
    implementation 'androidx.appcompat:appcompat:1.5.1'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.5.1'
    implementation 'androidx.webkit:webkit:1.5.0'
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.1"
    implementation 'org.jetbrains.kotlinx:kotlinx-serialization-json:1.3.2'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.4'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.0'
}

artifacts {
    archives sourcesJar
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                group rootProject.ext["GROUP"]
                artifactId 'peregrine'
                version rootProject.ext["VERSION"]

                afterEvaluate {
                    from components.release
                }

                pom {
                    name = "Peregrine"
                    description = "Peregrine for Android: native container for hybrid apps"
                    url = "https://peregrinejs.com"

                    licenses {
                        license {
                            name = "GNU General Public License version 3"
                            url = "https://opensource.org/licenses/gpl-3.0.html"
                        }
                    }

                    developers {
                        developer {
                            id = "imhoffd"
                            name = "Dan Imhoff"
                            email = "dwieeb@gmail.com"
                        }
                    }

                    scm {
                        connection = "scm:git:https://github.com/peregrinejs/Android.git"
                        developerConnection = "scm:git:ssh://git@github.com:peregrinejs/Android.git"
                        url = "https://github.com/peregrinejs/Android"
                    }
                }
            }
        }
    }
}

signing {
    useInMemoryPgpKeys(
            rootProject.ext["SIGNING_KEY_ID"],
            rootProject.ext["SIGNING_KEY"],
            rootProject.ext["SIGNING_PASSWORD"]
    )

    sign publishing.publications
}